Program = _{SOI ~ (Expr)* ~ EOI}

ImgUnit = {"i" | "I"}
Real = {ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)?}
Complex = {(Real ~ ImgUnit) | ImgUnit}
Number = _{Complex | Real}

BaseExpr = _{Number | FunctionCall | Abs | "("~ Expr ~")" | Var}
Expr = { UnaryPrefixOperator* ~ BaseExpr ~ UnaryPostfixOperator* ~ (BinaryOperator ~ UnaryPrefixOperator* ~ BaseExpr ~ UnaryPostfixOperator* )* }

BinaryOperator = _{Add | Sub | Mul | Div | Mod | Pow | LessEqualTo | GreaterEqualTo | LessThan | GreaterThan | Equal}
UnaryPrefixOperator = _{Plus | Neg}
UnaryPostfixOperator = _{Factorial}

Neg = {"-"}
Plus = {"+"}
Add = {"+"}
Sub = {"-"}
Mul = {"*"}
Div = {"/"}
Pow = {"^"}
Mod = {"%"}
LessThan = {"<"}
GreaterThan = {">"}
LessEqualTo = {"<="}
GreaterEqualTo = {">="}
Equal = {"="}
Factorial = {"!"}

Iden = {ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")*}
Var = { Iden }

FunctionCall = {Iden ~ "(" ~ (Expr ~ ("," ~ Expr )* ~ ")" | ")")}
Abs = {"|" ~ Expr ~ "|"}

COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!"\n" ~ ANY)* ~ NEWLINE) }
WHITESPACE = _{ " " | "\t" | "\n" | "\r"}